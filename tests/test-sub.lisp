;;; -*- Mode:Lisp; Syntax:ANSI-Common-Lisp; Coding:utf-8 -*-

(in-package #:cl-num-utils-tests)

(deftestsuite sub-tests (cl-num-utils-tests)
  ()
  (:equality-test #'equalp))

(addtest (sub-tests)
  test-sub
  (let ((a (ia 3 4)))
    (ensure-same (sub a (si 0 -1) (si 0 -1))
                 #2A((0 1 2)
                     (4 5 6)))
    (ensure-same (sub a (si 1 -1) t)
                 #2A((4 5 6 7)))
    (ensure-same (sub a 1 t)
                 #(4 5 6 7))
    (ensure-same (sub a (rev t) (cat (si 0 2) (si 2 4)))
                 #2A((8 9 10 11)
                     (4 5 6 7)
                     (0 1 2 3)))
    (ensure (not (equalp (sub a 1 t)
                         #2A((4 5 6 7)))))))

(addtest (sub-tests)
  test-setf-sub
  (let ((b (ia 2 3)))
    (let ((a (ia 3 4)))
      (ensure-same (setf (sub a (si 1 0) (si 1 0)) b) b)
      (ensure-same a #2A((0 1 2 3)
                         (4 0 1 2)
                         (8 3 4 5)))
      (ensure-same b (ia 2 3)))
    (let ((a (ia 3 4)))
      (ensure-same (setf (sub a (si 0 -1) #(3 2 1)) b) b)
      (ensure-same a #2A((0 2 1 0)
                         (4 5 4 3)
                         (8 9 10 11)))
      (ensure-same b (ia 2 3))
      (ensure-error (setf (sub a 2 4) (list 3)))
      (ensure-error (setf (sub a 2 4) (vector 3))))))

(addtest (sub-tests)
  test-sub-si
  (let ((a (iseq 10)))
    (ensure-same (sub a (si 0 0)) a)
    (ensure-same (sub a (si 0 0 1)) a)
    (ensure-same (sub a (si 0 0 2)) #(0 2 4 6 8))
    (ensure-same (sub a (si 0 9 2)) #(0 2 4 6 8))
    (ensure-same (sub a (si 0 8 2)) #(0 2 4 6))
    (ensure-same (sub a (si 1 9 2)) #(1 3 5 7))
    (ensure-same (sub a (si 1 -1 2)) #(1 3 5 7))
    (ensure-same (sub a (si 1 0 2)) #(1 3 5 7 9))
    (ensure-same (sub a (si 0 0 3)) #(0 3 6 9))
    (ensure-same (sub a (si 0 -1 3)) #(0 3 6))
    (ensure-same (sub a (si 1 -1 3)) #(1 4 7))
    (ensure-same (sub a (si 1 7 3)) #(1 4))
    (ensure-same (sub a (sub (rev (si 0 0 3)) #(0 1))) #(9 6))))

(addtest (sub-tests)
  map-columns
  (ensure-same (map-columns #'sum (ia 3 4))
               #(12 15 18 21))
  (ensure-same (map-columns (lambda (col) (vector (sum col) (mean col))) (ia 3 4))
               #2A((12 15 18 21)
                   (4 5 6 7))))

(addtest (sub-tests)
  map-rows
  (ensure-same (map-rows #'sum (ia 4 3))
               #(3 12 21 30))
  (ensure-same (map-rows (lambda (col) (vector (sum col) (mean col))) (ia 4 3))
               #2A((3 1)
                   (12 4)
                   (21 7)
                   (30 10))))

(addtest (sub-tests)
  reshape-calculate-dimensions
  (ensure-same (reshape-calculate-dimensions #(1 2 3) 6) #(1 2 3))
  (ensure-same (reshape-calculate-dimensions #(1 t 3) 6) #(1 2 3))
  (ensure-same (reshape-calculate-dimensions #(1 t 3) 0) #(1 0 3))
  (ensure-same (reshape-calculate-dimensions 6 6) #(6))
  (ensure-same (reshape-calculate-dimensions t 6) #(6))
  (ensure-error (reshape-calculate-dimensions #(1 t t 3) 6))
  (ensure-error (reshape-calculate-dimensions #(1 t 0 3) 6)))

(addtest (sub-tests)
  reshape
  (let ((a (ia 3 4))
        (a-reshaped-rm #2A((0 1 2)
                           (3 4 5)
                           (6 7 8)
                           (9 10 11))))
    (ensure-same (reshape a '(4 t) :row-major) a-reshaped-rm)
    (ensure-same (reshape a '(4 t) :row-major :copy t) a-reshaped-rm)
    (ensure-same (reshape a '(4 t) :column-major) 
                 #2A((0 5 10)
                     (4 9 3)
                     (8 2 7)
                     (1 6 11)))))

(addtest (sub-tests)
  rows-and-columns
  (let ((a #2A((1 2)
               (3 4)
               (5 6)))
        (rows (list #(1 2) #(3 4) #(5 6)))
        (columns (list #(1 3 5) #(2 4 6))))
    (ensure-same (rows a) rows)
    (ensure-same (columns a) columns)))

(addtest (sub-tests)
  pref
  (let ((matrix #2A((0 1)
                    (2 3)
                    (4 5)))
        (vector #(0 1 2 3)))
    (ensure-same (pref matrix #(0 2 1) #(1 0 1)) #(1 4 3))
    (ensure-same (pref vector #(3 1 2 0)) #(3 1 2 0))
    (ensure-error (pref vector #(1) #(0)))
    (ensure-error (pref matrix #(1 0) #(0)))))

(addtest (sub-tests)
  which
  (let* ((vector #(7 6 5 4 3 2 1 0))
         (list (coerce vector 'list))
         (even-pos #(1 3 5 7))
         (odd-pos #(0 2 4 6))
         (arbitrary (reverse #(0 2 3 5)))
         (arbitrary-pos #(2 4 5 7)))
    (ensure-same (which #'oddp vector) odd-pos)
    (ensure-same (which #'oddp list) odd-pos)
    (ensure-same (which #'evenp vector) even-pos)
    (ensure-same (which #'evenp list) even-pos)
    (flet ((in? (element) (find element arbitrary)))
      (ensure-same (which #'in? vector) arbitrary-pos)
      (ensure-same (which #'in? list) arbitrary-pos))))

(addtest (sub-tests)
  bitspec
  (let* ((vector (iseq 6))
        (odd-bits (bitmap #'oddp vector))
        (even-bits (bitmap #'evenp vector))
        (div3-bits (bitmap (lambda (n) (divides? n 3)) vector)))
    (ensure-same even-bits #*101010)
    (ensure-same odd-bits #*010101)
    (ensure-same div3-bits #*100100)
    (ensure-same (sub vector even-bits) #(0 2 4))
    (ensure-same (sub vector odd-bits) #(1 3 5))
    (ensure-same (sub vector div3-bits) #(0 3))
    (ensure-same (sub vector (bit-ior even-bits div3-bits)) #(0 2 3 4))))
